= Quickstart

:idprefix:
:idseparator: -
:sectlinks:


:uri-repo: https://github.com/oracle-terraform-modules/terraform-oci-vcn
:uri-rel-file-base: link:{uri-repo}/blob/master
:uri-rel-tree-base: link:{uri-repo}/tree/master
:uri-docs: {uri-rel-file-base}/docs
:uri-oci-keys: https://docs.cloud.oracle.com/iaas/Content/API/Concepts/apisigningkey.htm
:uri-oci-ocids: https://docs.cloud.oracle.com/iaas/Content/API/Concepts/apisigningkey.htm#five
:uri-terraform: https://www.terraform.io
:uri-terraform-oci: https://www.terraform.io/docs/providers/oci/index.html
:uri-terraform-options: {uri-docs}/terraformoptions.adoc
:uri-variables: {uri-rel-file-base}/variables.tf

. link:#assumptions[Assumptions]
. link:#pre-requisites[Pre-requisites]
. link:#instructions[Instructions]
. link:#related-documentation[Related documentation]

=== Assumptions

1. You have setup the {uri-oci-keys}[required keys]
2. You know the {uri-oci-ocids}[required OCIDs]
3. You have the necessary permissions

=== Pre-requisites

1. git is installed
2. ssh client is installed
3. Terraform 0.12.24+ is installed

=== Provisioning using this git repo

. Clone the repo:

+
[source,bash]
----
git clone https://github.com/oracle-terraform-modules/terraform-oci-vcn.git tfvcn

cd tfvcn

cp terraform.tfvars.example terraform.tfvars
----

. Create a provider.tf file and add the following:

+
[source,hcl]
----
provider "oci" {
  fingerprint          = var.api_fingerprint
  private_key_path     = var.api_private_key_path
  region               = var.region
  tenancy_ocid         = var.tenancy_id
  user_ocid            = var.user_id
}

# provider identity parameters
variable "api_fingerprint" {
  description = "fingerprint of oci api private key"
  type        = string
}

variable "api_private_key_path" {
  description = "path to oci api private key used"
  type        = string
}

variable "tenancy_id" {
  description = "tenancy id where to create the sources"
  type        = string
}

variable "user_id" {
  description = "id of user that terraform will use to create the resources"
  type        = string
}
----

. Set mandatory provider parameters in terraform.tfvars:

* `api_fingerprint`
* `api_private_key_path`
* `region`
* `tenancy_id`
* `user_id`



. Override other parameters in terraform.tfvars:

* `compartment_id`
* `label_prefix`
* `vcn_dns_label`
* `vcn_name`

. Optional parameters to override:

* `internet_gateway_enabled`
* `nat_gateway_enabled`
* `service_gateway_enabled`
* `tags`

. Run Terraform:

+
[source,bash]
----
terraform init
terraform plan
terraform apply
----

=== Provisioning using the HashiCorp registry module

You can also use the method below to reuse this in your own module.

. Create a variable.tf file in your root project and add the following:

+
----
# provider identity parameters
variable "api_fingerprint" {
  description = "fingerprint of oci api private key"
  type        = string
}

variable "api_private_key_path" {
  description = "path to oci api private key used"
  type        = string
}

variable "region" {
  description = "the oci region where resources will be created"
  type        = string
}

variable "tenancy_id" {
  description = "tenancy id where to create the sources"
  type        = string
}

variable "user_id" {
  description = "id of user that terraform will use to create the resources"
  type        = string
}

# general oci parameters

variable "compartment_id" {
  description = "compartment id where to create all resources"
  type        = string
}

variable "label_prefix" {
  description = "a string that will be prepended to all resources"
  type        = string
}

# vcn parameters

variable "internet_gateway_enabled" {
  description = "whether to create the internet gateway"
  default     = false
  type        = bool
}

variable "nat_gateway_enabled" {
  description = "whether to create a nat gateway in the vcn"
  default     = false
  type        = bool
}

variable "service_gateway_enabled" {
  description = "whether to create a service gateway"
  default     = false
  type        = bool
}

variable "tags" {
  description = "simple key-value pairs to tag the resources created"
  type        = map(any)
  default = {
    "environment" = "dev"
  }
}

variable "vcn_cidr" {
  description = "cidr block of VCN"
  default     = "10.0.0.0/16"
  type        = string
}

variable "vcn_dns_label" {
  description = "A DNS label for the VCN, used in conjunction with the VNIC's hostname and subnet's DNS label to form a fully qualified domain name (FQDN) for each VNIC within this subnet"
  type        = string
}

variable "vcn_name" {
  description = "user-friendly name of to use for the vcn to be appended to the label_prefix"
  type        = string
}
----

. Create a provider.tf file in your root directory and add the following:

+
[source,hcl]
----
provider "oci" {
  fingerprint          = var.api_fingerprint
  private_key_path     = var.api_private_key_path
  region               = var.region
  tenancy_ocid         = var.tenancy_id
  user_ocid            = var.user_id
}
----

. Create a main.tf file and add the following:

+
[source,hcl]
----
module "vcn" {
  source  = "oracle-terraform-modules/vcn/oci"
  version = "1.0.2"

  # provider parameters
  region = var.region

  # general oci parameters
  compartment_id = var.compartment_id
  label_prefix   = var.label_prefix

  # vcn parameters
  internet_gateway_enabled = var.internet_gateway_enabled
  nat_gateway_enabled      = var.nat_gateway_enabled
  service_gateway_enabled  = var.service_gateway_enabled
  tags                     = var.tags
  vcn_cidr                 = var.vcn_cidr
  vcn_dns_label            = var.vcn_dns_label
  vcn_name                 = var.vcn_name
}
----

. Copy terraform.tfvars.example to terraform.tfvars and set the mandatory provider parameters:

+
[source,bash]
----
cp terraform.tfvars.example terraform.tfvars
----

* `api_fingerprint`
* `api_private_key_path`
* `region`
* `tenancy_id`
* `user_id`

. Override other parameters in terraform.tfvars:

* `compartment_id`
* `label_prefix`
* `vcn_dns_label`
* `vcn_name`

. Optional parameters to override:

* `internet_gateway_enabled`
* `nat_gateway_enabled`
* `service_gateway_enabled`
* `tags`

. Run Terraform:

+
[source,bash]
----
terraform init
terraform plan
terraform apply
----

=== Related documentation:

* {uri-terraform-options}[All Terraform configuration options] for {uri-repo}[this project]
